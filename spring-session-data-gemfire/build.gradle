plugins {
  id 'java-library'
  id 'maven-publish'
  id "io.freefair.lombok" version "8.0.1"
  id 'nebula.facet' version "9.6.3"
  id 'dependency-constraints'
  id 'gemfire-repo-artifact-publishing'
  id 'spring-java-jar'
}

description = 'Spring Session for VMware GemFire'
project.ext.set("pomProjectLongName","Spring Session VMware GemFire")
project.ext.set("pomProjectArtifactName","spring-session-2.7-gemfire-${gemfireBaseVersion}")
project.ext.set("pomProjectDescription","Spring Session For VMware GemFire")

facets {
  integrationTest {
    includeInCheckLifecycle = false
  }
}

dependencies {
  implementation 'org.springframework:spring-context-support'
  implementation 'org.springframework:spring-jcl'

  api "com.vmware.gemfire:spring-data-2.7-gemfire-${gemfireBaseVersion}:${springDataGemFireVersion}"
  api 'org.springframework.session:spring-session-core:2.7.1'


  implementation 'com.google.code.findbugs:jsr305'
  implementation 'org.springframework.security:spring-security-core'
  implementation 'org.springframework.security:spring-security-web'
  implementation 'org.springframework:spring-tx'
  compileOnly "com.vmware.gemfire:geode-server-all:$gemfireVersion"

  runtimeOnly "jakarta.servlet:jakarta.servlet-api"

  testImplementation "com.vmware.gemfire:geode-server-all:$gemfireVersion"

  testImplementation 'edu.umd.cs.mtc:multithreadedtc'
  testImplementation("com.vmware.gemfire:spring-test-gemfire-9.15-2.7:$springTestGemFireVersion")

  testImplementation 'org.assertj:assertj-core'
  testImplementation 'junit:junit'
  testImplementation 'org.mockito:mockito-core'
  testImplementation 'org.springframework:spring-test'
  testImplementation 'org.springframework:spring-web'
  testImplementation 'ch.qos.logback:logback-classic'
  testImplementation 'org.slf4j:log4j-over-slf4j'
  compileOnly 'jakarta.servlet:jakarta.servlet-api'
  testCompileOnly 'jakarta.servlet:jakarta.servlet-api'

  integrationTestImplementation 'junit:junit'
  integrationTestImplementation 'org.assertj:assertj-core'
  integrationTestImplementation 'ch.qos.logback:logback-classic'
  integrationTestImplementation 'org.slf4j:log4j-over-slf4j'
  integrationTestImplementation 'com.google.code.findbugs:jsr305'
  integrationTestImplementation 'org.springframework.shell:spring-shell'
  integrationTestImplementation 'org.springframework:spring-test'
  integrationTestImplementation("com.vmware.gemfire:spring-test-gemfire-9.15-2.7:$springTestGemFireVersion")
}

tasks.named("processIntegrationTestResources") {
  duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

sourceSets {
  integrationTest {
    java {
      srcDir file('src/integrationTest/java')
    }
    resources {
      srcDir file('src/integrationTest/resources')
    }
  }
}

integrationTest{
  forkEvery = 1
  maxParallelForks = 1

  filter {
    includeTestsMatching "*.*Tests"
    includeTestsMatching "*.*Test"
  }
}

test {
  forkEvery = 1
  maxParallelForks = 1

  filter {
    includeTestsMatching "*.*Tests"
    includeTestsMatching "*.*Test"
  }
}

repositories {
  def additionalMavenRepoURLs = project.ext.get('additionalMavenRepoURLs')
  if (!additionalMavenRepoURLs.isEmpty() && !additionalMavenRepoURLs.isBlank()) {
    additionalMavenRepoURLs.split(",").each {
      project.getRepositories()
              .maven(mavenRepository -> {
                mavenRepository.setUrl(uri(it.toString()));
              });
    }
  }
}
